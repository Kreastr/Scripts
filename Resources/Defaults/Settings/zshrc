#!/bin/zsh

source GoboPath

### Global ZShell settings.

# Disable flow control. (Ctrl-S, Ctrl-Q is annoying)
stty -ixon

# Shell options:
setopt autopushd autocd nobeep nohashcmds histignoredups completealiases
autoload run-help
unalias run-help 2> /dev/null

alias ll='ls -la'
alias ls='ls --color=tty'
alias lsd='find -type d -maxdepth 1'
alias lsh='ls -A .* -d'
alias mkdir='mkdir -p'
alias off='sudo -u "#0" ${goboExecutables}/shutdown -h -t 0 now "GoboLinux is powering off now."'
alias halt='off'
alias poweroff='off'
alias pd='pushd'
alias p='popd'
alias grep='grep --color=tty'
# An alias for the lousy typist :)
alias sl='ls'

if [ "$goboPrefix" ]
then goboUserPrefix=${goboPrefix}
else goboUserPrefix=$HOME
fi

export HISTCONTROL=ignoredups
export HISTIGNORE="ls:exit"
export HISTFILE="${XDG_CACHE_HOME:-"${goboUserPrefix}/.cache"}/zsh_history"
export HISTSIZE=100
export SAVEHIST=200

export MANPATH="${goboManuals}:${goboShared}/man${MANPATH:+:$MANPATH}:$(manpath 2> /dev/null)"
export MANPATH="${MANPATH%:}"
export INFOPATH="${goboManuals}/info${INFOPATH:+:$INFOPATH}"
export TMPDIR="${goboTemp}"

path=("${goboExecutables}" $path)
fpath=("${goboShared}/zsh/functions" "${XDG_DATA_HOME:-"${goboUserPrefix}/.local/share"}/zsh/functions" "${XDG_CONFIG_HOME:-"${goboUserPrefix}/.config"}/zsh/functions" "${goboUserPrefix}/.Settings/zsh/functions" $fpath)

unset goboUserPrefix

autoload -U promptinit
promptinit
autoload colors
colors

# This line has to come first for dircolors.
export SHELL=`which zsh`
eval `dircolors ${goboSettings}/dircolors.conf 2> /dev/null`

# The following lines were added by compinstall

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' list-colors 'no=00:fi=00:di=01;34:ln=01;36:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.gz=01;31:*.bz2=01;31:*.deb=01;31:*.rpm=01;31:*.jpg=01;35:*.png=01;35:*.gif=01;35:*.bmp=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.png=01;35:*.mpg=01;35:*.avi=01;35:*.fli=01;35:*.gl=01;35:*.dl=01;35:'
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
# Partly insensitive matcher:
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' ''
# Completely insensitive matcher:
#zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' ''
zstyle ':completion:*' menu select=1
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
#zstyle :compinstall filename '${goboSettings}/zshrc'

autoload -U compinit
compinit -i
# End of lines added by compinstall

# This must be done before loading a zkbd
zmodload zsh/complist &> /dev/null

if [[ "$VENDOR" = "unknown" ]]
then
   export VENDOR=pc
fi

if [[ -e ${goboShared}/zsh/zkbd-$TERM-$VENDOR-$OSTYPE ]]
then
   . ${goboShared}/zsh/zkbd-$TERM-$VENDOR-$OSTYPE

   #bindkey "${key[CtrlUp]}" up-history
   #bindkey "${key[CtrlDown]}" down-history
   #bindkey "${key[CtrlLeft]}" backward-word
   #bindkey "${key[CtrlRight]}" forward-word

   bindkey "${key[Home]}" beginning-of-line
   bindkey "${key[End]}" end-of-line
   bindkey "${key[Insert]}" redisplay
   bindkey "${key[Delete]}" delete-char-or-list
   bindkey -M menuselect "${key[Delete]}" undo

   bindkey "${key[PageUp]}" undo
   bindkey "${key[PageDown]}" redo

   # Alt-Backspace: delete word backwards
   bindkey '^[^?' backward-kill-word

   # How about this: context-sensitive help on the shell :)
   bindkey "${key[F1]}" run-help
fi

# Try pressing "/" when TAB-completing your way through a long pathname.
bindkey -M menuselect "/" accept-and-infer-next-history

# Alt+Space: complete word based on history.
bindkey "^[ " _history-complete-older

# For the Windows users out there -- I like PageUp and PageDown better.
bindkey '^Z' undo

# Bash-like behavior that is nice.
autoload bash-backward-kill-word
autoload bash-kill-word
zle -N backward-kill-word bash-backward-kill-word
zle -N kill-word bash-kill-word

# Zsh is nice and provides a "which", but "no, thanks, we have our own".
disable which

# Source application environment settings.
local envcachefile="${goboEnvironment}/Cache"
[[ -e "$envcachefile" ]] || { 
echo -e "unset unsetvars\n[ ! \"\$goboSettings\" ] && unsetvars=true\n" > "$envcachefile"
echo -e "source GoboPath\n" >> "$envcachefile"
for linkedfile in "${goboEnvironment}"/*--*
do 
   [ -f "$linkedfile" ] && cat "$linkedfile" >> "$envcachefile"
   echo >> "$envcachefile"
done
unsetvars=`grep export ${goboExecutables}/GoboPath | awk {'print $1 " " $2'} | grep "^export" | sed -e 's,=.*,,g' -e 's,export,unset,g'`
echo -e "if [ \"\$unsetvars\" ]\nthen\n$unsetvars\nfi\n" >> "$envcachefile" } 2> /dev/null
{ source "$envcachefile" } 2> /dev/null
unset envcachefile

# CommandNotFound code is in a separate file so it can be updated outside of
# an UpdateSettings run.
source "${goboPrefix}/Programs/Scripts/Current/Functions/CommandNotFound"

source "${goboPrefix}/Programs/Scripts/Current/share/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
ZSH_HIGHLIGHT_STYLES[globbing]='fg=cyan'
